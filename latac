#!/usr/bin/gawk -f

##################################################
# File:
#       latac
#
# Maintainer:
#       Sepp Tannhuber
#
# Version:
#       1
#
# Sections:
#    -> Table font definitions
#    -> Awk field seperators
#    -> Basic table layout (begin)
#    -> Line formatting
#    -> Basic table layout (end)
##################################################

BEGIN{
    ##############################################
    # => Table font definitions
    ##############################################
    HEADERFONT="\\\\footnotesize\\\\sffamily\\\\bfseries "
    FONT="\\small"

    ##############################################
    # => Awk field seperators
    ##############################################
    FS="&"
    ORS=""

    ##############################################
    # => Basic table layout (begin)
    ##############################################
    print "\\begin{table}[]\n\t"
    print "\\caption{}\n\t"
    print "\\smallskipamount=1pt\n\t"
    print "\\centering\n\t"

    # Set table body font
    print FONT"\n\t\t"
}
{
    # Calculate number of columns and print rest of table header
    if(NR==1) {
        for(i=1; i<=NF; i++) {
            if($i ~ /:[[:digit:]+]$/)
                COLFORMAT[i]=gensub(/.*:([[:digit:]+])/, "*{\\1}l", "g", $i)
            if($i ~ /:[[:digit:]+]\$/)
                COLFORMAT[i]=gensub(/.*:([[:digit:]+])\$/, "*{\\1}{>$r<$}", "g", $i)
            if($i ~ /:\$/)
                COLFORMAT[i]=gensub(/.*/, ">$r<$", "g", $i)
            if($i ~ /:[[:digit:]+][^$\s]/)
                COLFORMAT[i]=gensub(/.*:([[:digit:]+])(.)/, "*{\\1}{\\2}", "g", $i)
            if($i ~ /:[^[:digit:]$]+/)
                COLFORMAT[i]=gensub(/.*:(.*)/, "\\1", "g", $i)
            if(COLFORMAT[i]=="")
                COLFORMAT[i]="l"
        }
        print "\\begin{tabular}{"
        for(i in COLFORMAT) {
            print COLFORMAT[i]
            COLS=gensub(/.*\*\{([[:digit:]+])\}.*/, "\\1", "g", COLFORMAT[i])
            if(COLS !~ /^[[:digit:]+]$/)
                COLS=1
            COLNUMS+=COLS
        }
        print "}\n\t\t"
        print "\\toprule\n\t\t"
    }
    ##############################################
    # => Line formatting
    ##############################################
    for(i=1; i<=NF; i++) {
        if($i ~ /[[:digit:]*]-[[:digit:]*]/)
            a=$i
        else
            a=gensub(/(.+)/, HEADERFONT"\\1", "g", $i)

        # Format the midrules, alignment depends on position
        firstcol=gensub(/([[:digit:]+])-[[:digit:]+]/, "\\1", "g", a)
        lastcol=gensub(/[[:digit:]+]-([[:digit:]+])/, "\\1", "g", a)
        if(firstcol==1)
            f=""
        else
            f="l"
        if(lastcol==COLNUMS)
            l=""
        else
            l="r"

        b=gensub(/([[:digit:]+]-[[:digit:]+])/, "\\\\cmidrule("f l"){\\1}", "g", a)

        # Format multicolumns, always left aligned
        c=gensub(/(.+):([[:digit:]+]).*/, "\\\\multicolumn{\\2}{l}{\\1}", "g", b)

        if(NR != 1 || $1 !~ /^:/)
            print c

        # Insert field separators and line breaks
        if($i ~ /[[:digit:]*]-[[:digit:]*]/)
        {
            if(i==NF)
            {
                print "\n\t\t"
                HEADERFONT=""
            }
        }
        else {
            if(i<NF) {
                if(HEADERFONT != "")
                    separator="&\n\t\t"
                else
                    separator="& "
            }
            else
                separator="\\\\\n\t\t"
            if(NR != 1 || $1 !~ /^:/)
                print separator
        }
    }
}
##############################################
# => Basic table layout (end)
##############################################
END{
    print "\\bottomrule\n\t"
    print "\\end{tabular}"
    print "\\label{tbl:}\n"
    print "\\end{table}"
}
